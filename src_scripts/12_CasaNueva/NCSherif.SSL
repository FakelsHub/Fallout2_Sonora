#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_NCSherif
#define TOWN_REP_VAR                    GVAR_TOWN_REP_CASA_NUEVA // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CASANUEVA_PEASAN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_BOS_PALADIN        // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      100 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeStopDialog;
procedure NodeStopDialog2;
procedure NodeStopDialog3;
procedure NodeStopDialog4;
procedure NodeStopDialog5;
procedure NodeStopDialog6;
procedure NodeStopDialog7;
procedure NodeStopDialog8;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer103;
procedure NodeAnswer104;
procedure NodeAnswer105;
procedure NodeAnswer106;
procedure NodeAnswer107;
procedure NodeAnswer108;
procedure NodeAnswer200;
procedure NodeAnswer201;
procedure NodeAnswer202;
procedure NodeAnswer203;
procedure NodeAnswer300;
procedure NodeAnswer301;
procedure NodeAnswer302;
procedure NodeAnswer303;
procedure NodeAnswer304;
procedure NodeAnswer305;
procedure NodeAnswer306;
procedure NodeAnswer307;
procedure NodeAnswer308;
procedure NodeAnswer309;
procedure NodeAnswer310;
procedure NodeAnswer311;
procedure NodeAnswer312;
procedure NodeAnswer313;
procedure NodeAnswer314;

//---------------------

#define LVAR_Timer_Curfew              (6) // время до следующего задержания в комендантский час (5 минут)
#define LVAR_Weapon_Timer              (7) // таймер проверки в руках ГГ оружия

import variable box_stop_use;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_local_var(LVAR_Timer_Curfew,0);
       if (getQuest(GVAR_PUERTO_BOS_DESTROY) >= qEnd) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       destroy_object(self_obj);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (game_time_hour < 0600) then begin
       timer_move_patrol(23099,16299)
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
   if (fixed_param == 3) then begin
       setQuest(GVAR_NUEVA_QST_EL_GIGANTO,qStart);
       dude_teleport_map(1,1,MAP_CASA_NUEVA_CEMENT,3);
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if (game_time_hour < 0600) then begin
      if ((global_var(GVAR_TITUL_BOS)>0) or town_rep_is_idolized or (getReaction>=75)) then begin
      end
      else if (local_var(LVAR_Timer_Curfew) < game_time) then begin
         if self_can_see_dude then begin
         dialogue_system_forcing(10)
         end
      end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,2,2,2) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (getQuest(GVAR_NUEVA_QST_EL_GIGANTO) < qEnd) then begin
   setQuest(GVAR_NUEVA_QST_EL_GIGANTO,qZero);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   fracReaction(GVAR_TITUL_BOS,50) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_WARRIOR_ROAD,-25) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_DUDE_IS_SLAVE,-25) // Поправка к отношению за принадлежность к фракциям
   dialog_proc(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (game_time_hour < 0600) then begin
   call NodeStopDialog;
   end
   else if ((bitDialog(bit_5) == 0) and (global_var(GVAR_TOWN_REP_PUERTO)<=-30)) then begin
   call NodeStopDialog6;
   end
   else if ((bitDialog(bit_2) == 0) and (global_var(GVAR_TITUL_BOS)>0)) then begin
   call NodeFstDialog3;
   end
   else if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- СТОП-ДИАЛОГ

procedure NodeStopDialog begin
   if ((global_var(GVAR_TITUL_BOS)>0) or town_rep_is_idolized or (getReaction>=75)) then begin
      Reply(254)
      NAnswer(255,1,NodeStopDialog2)
   end
   else if (local_var(LVAR_Timer_Curfew) > game_time) then begin
      Reply(258)
      NAnswer(259,1,Node999)
   end
   else begin
      if (map_var(MVAR_NUEVA_CURFEW) >= 6) then begin
      Reply(265)
      NAnswer(266,1,Node998)
      BAnswer(267,1,Node998)
      end
      else if ((map_var(MVAR_NUEVA_CURFEW) == 2) or (map_var(MVAR_NUEVA_CURFEW) == 5)) then begin
      Reply(262)
      NAnswer(263,1,NodeStopDialog4)
      BAnswer(264,1,Node998)
      end
      else if (map_var(MVAR_NUEVA_CURFEW) >0) then begin
      Reply(260)
      NAnswer(261,1,NodeStopDialog3)
      end
      else begin
      Reply(250)
      NAnswer(251,1,NodeStopDialog3)
      end
   end
end

procedure NodeStopDialog2 begin
   setBonusReaction(-5)
   Reply(256)
   NAnswer(257,1,Node999)
end

procedure NodeStopDialog3 begin
   call NodeStopDialog5;
   Reply(252)
   NAnswer(253,1,Node999)
end

procedure NodeStopDialog4 begin
   set_REP(-5)
   gfade_out(ONE_GAME_SECOND);
   inven_unwield(dude_obj);
   move_to(dude_obj,23079,0);
   game_time_advance(ONE_GAME_HOUR*10);
   gfade_in(ONE_GAME_SECOND);
   call NodeStopDialog5;
   call Node999;
end

procedure NodeStopDialog5 begin
   inc_map_var(MVAR_NUEVA_CURFEW);
   set_local_var(LVAR_Timer_Curfew, (ONE_GAME_MINUTE*2) + game_time);
   setBonusReaction(-10)
end

procedure NodeStopDialog6 begin
   bitDialogExp(bit_5,0)
   Reply(270)
   NAnswerSkill(275,3,NodeStopDialog8,SKILL_CONVERSANT,80,274,NodeStopDialog7)
   NAnswer(271,1,NodeStopDialog7)
end

procedure NodeStopDialog7 begin
   Reply(272)
   NAnswer(273,1,Node998)
end

procedure NodeStopDialog8 begin
   Reply(276)
   NAnswer(277,1,Node999)
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(203,1,NodeSndDialog)
end

procedure NodeFstDialog3 begin
   bitDialogExp(bit_1,0)
   bitDialogExp(bit_2,0)
   Reply(204)
   NAnswer(205,1,NodeFstDialog4)
end

procedure NodeFstDialog4 begin
   Reply(206)
   NAnswer(207,1,NodeSndDialog)
end

//--- ВТОРОЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (getReaction>=20) then begin
   Reply(211)
   end
   if (getReaction<0) then begin
   Reply(212)
   end
   else begin
   Reply(210)
   end

   //--- Рядовые расспросы
   if (bitDialog(bit_4) == 0) then begin
   NAnswer(230,3,NodeAnswer200)
   end
   NAnswer(280,3,NodeAnswer300)
   NAnswer(283,3,NodeAnswer301)
   NAnswer(286,3,NodeAnswer302)
   if (getQuest(GVAR_NUEVA_QST_EL_GIGANTO) >= qEnd) then begin
   NAnswer(312,5,NodeAnswer311)
   end
   if (bitDialog(bit_7) != 0) then begin
   if (bitDialog(bit_8) == 0) then begin
   NAnswer(319,3,NodeAnswer314)
   end
   end

   //--- Эль Гиганто
   if (getQuest(GVAR_NUEVA_QST_EL_GIGANTO) < qEnd) then begin
      if ((bitDialog(bit_6) != 0) or (getQuest(GVAR_NUEVA_QST_EL_GIGANTO) != qZero)) then begin
      NAnswer(300,3,NodeAnswer306)
      end
   end

   //--- Квест пропавшего на плантации
   if (getQuest(GVAR_NUEVA_QST_PLANT_DEAD) == qStart) then begin
   if (bitDialog(bit_3) == 0) then begin
   BAnswer(220,3,NodeAnswer100)
   end
   end

   //--- Пинки
   if (getQuest(GVAR_NUEVA_QST_PINKI) == qStart) then begin
      if (bitDialog(bit_9) == 0) then begin
         NAnswer(330,3,NodeAnswer102)
      end
      else begin
         if (global_var(GVAR_NUEVA_STATUS_PINKI) < 0) then begin
         GAnswer(343,3,NodeAnswer107)
         end
      end
   end

   NAnswer(213,1,Node999)
end

procedure NodeAnswer100 begin
   bitDialogExp(bit_3,0)
   Reply(221)
   NAnswer(222,1,NodeAnswer101)
end

procedure NodeAnswer101 begin
   Reply(223)
   NAnswer(224,1,Node999)
   NAnswer(225,1,Node999)
end

procedure NodeAnswer102 begin
   bitDialogExp(bit_9,0)
   Reply(331)
   NAnswer(332,1,NodeAnswer103)
end

procedure NodeAnswer103 begin
   Reply(333)
   NAnswer(334,1,NodeAnswer104)
   NAnswer(335,1,NodeAnswer104)
end

procedure NodeAnswer104 begin
   Reply(336)
   NAnswer(337,1,NodeAnswer105)
   NAnswer(340,1,NodeAnswer106)
end

procedure NodeAnswer105 begin
   Reply(338)
   NAnswer(339,1,Node999)
end

procedure NodeAnswer106 begin
   Reply(341)
   NAnswer(342,1,Node999)
end

procedure NodeAnswer107 begin
   Reply(344)
   NAnswer(345,1,NodeAnswer108)
   NAnswer(346,1,NodeAnswer108)
end

procedure NodeAnswer108 begin
   setQuest(GVAR_NUEVA_QST_PINKI,qEnd);
   set_REP(3)
   exp_add(250);
   item_add(dude_obj,PID_BOOTLE_CAPS,30)
   Reply(347)
   NAnswer(348,1,Node999)
end

procedure NodeAnswer200 begin
   bitDialogExp(bit_4,0)

   if (global_var(GVAR_TITUL_BOS)>0) then begin
   Reply(241)
   NAnswer(242,1,NodeSndDialog)
   end
   else begin
   Reply(231)
   NAnswer(232,1,NodeAnswer201)
   end
end

procedure NodeAnswer201 begin
   Reply(233)
   NAnswer(235,1,NodeAnswer202)
   NAnswer(234,1,Node999)
end

procedure NodeAnswer202 begin
   Reply(236)
   NAnswer(237,1,NodeAnswer203)
end

procedure NodeAnswer203 begin
   Reply(238)
   BAnswer(239,1,Node999)
   NAnswer(240,1,Node999)
end

procedure NodeAnswer300 begin
   Reply(281)
   NAnswer(282,1,NodeSndDialog)
end

procedure NodeAnswer301 begin
   Reply(284)
   NAnswer(285,1,NodeSndDialog)
end

procedure NodeAnswer302 begin
   Reply(287) // Я отвечаю за безопасность и закон. Видишь решетку? В этой камере окажется любой смутьян, которому не нравится местный порядок. Имей в виду.
   NAnswer(289,3,NodeAnswer303)
   NAnswer(293,-3,NodeAnswer304)
   NAnswer(288,1,NodeSndDialog)
end

procedure NodeAnswer303 begin
   Reply(290)
   BAnswer(291,3,NodeAnswer304)
   NAnswer(292,1,NodeSndDialog)
end

procedure NodeAnswer304 begin
   if ((bitDialog(bit_6) == 0) or (getQuest(GVAR_NUEVA_QST_EL_GIGANTO) == qZero)) then begin
   Reply(294)
   NAnswer(295,1,NodeAnswer305)
   end
   else begin
   call NodeSndDialog;
   end
end

procedure NodeAnswer305 begin
   bitDialogExp(bit_6,0)
   Reply(296)
   NAnswer(297,1,NodeSndDialog)
end

procedure NodeAnswer306 begin
   Reply(301)
   if (getQuest(GVAR_NUEVA_QST_EL_GIGANTO) == qZero) then begin
   NAnswer(303,3,NodeAnswer307)
   GAnswer(322,3,NodeAnswer307)
   end
   else if (global_var(GVAR_NUEVA_STATUS_EL_GIGANTO) <0) then begin
   GAnswer(307,3,NodeAnswer309)
   end
   NAnswer(302,1,NodeSndDialog)
end

procedure NodeAnswer307 begin
   Reply(304)
   NAnswer(305,1,NodeAnswer308)
   NAnswer(306,1,NodeSndDialog)
end

procedure NodeAnswer308 begin
   add_timer_event(self_obj, 2, 3);
   call Node999;
end

procedure NodeAnswer309 begin
   PLAY_FADE
   Reply(308)
   NAnswer(309,1,NodeAnswer310)
end

procedure NodeAnswer310 begin
   item_add(dude_obj,PID_BOOTLE_CAPS,300)
   setQuest(GVAR_NUEVA_QST_EL_GIGANTO,qEnd);
   set_REP(10)
   setBonusReaction(75)
   exp_add(500);
   Reply(310)
   NAnswer(311,1,Node999)
end

procedure NodeAnswer311 begin
   Reply(313)
   NAnswer(314,1,NodeAnswer312)
end

procedure NodeAnswer312 begin
   Reply(315)
   NAnswer(316,1,NodeAnswer313)
end

procedure NodeAnswer313 begin
   bitDialogExp(bit_7,0)
   Reply(317)
   NAnswer(318,1,NodeSndDialog)
end

procedure NodeAnswer314 begin
   bitDialogExp(bit_8,50)
   Reply(320)
   NAnswer(321,1,NodeSndDialog)
end
